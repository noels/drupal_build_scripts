/**
set up a new project (set up repository, database, vhost)
manage developers that have access to the repository
backup and deploy project from developing server to staging server, or from staging to live
make backup & restore backup
*/

import org.apache.tools.ant.filters.FixCrLfFilter
import org.apache.tools.ant.filters.ReplaceTokens

ext.propertyFiles = [
        "${ant.properties.env}.secure.properties",
        "secure.properties",
        "${ant.properties.site}.properties",
        "${ant.properties.env}.properties",
        "build.properties"
]
ant.importBuild "build.xml"
apply from: "runCmd.gradle"
apply from: "drush.gradle"
apply from: "database.gradle"
apply from: "deploy.gradle"

println "build dir is:  ${ant.properties.build}"
ext.buildApplicationRoot = "${ant.properties.build}/${ant.properties."application.name"}/build_${ant.properties.BUILD_NUMBER}"

task init {
    inputs.files propertyFiles
    outputs.file ant.properties."drupal.filesTarget"
    doFirst{
        //create the files directory target as gradle can't handle dangling symlinks
        runCmd("mkdir -p ${ant.properties."drupal.filesTarget"}", new File(".") )
    }
}

task clean << {
    new File("remote.output").delete()
    runCmd("""/bin/rm -rf ${ant.properties.build}""", new File("."))
    runCmd("""/bin/rm -rf ${ant.properties.target}""", new File("."))
}

/* this is a customised copy task which is why there is a slightly different syntax for defining it.
	Note the missing <<.
*/
task copySrc(type: Copy, dependsOn: init) {
	from("${ant.properties.src}"){
		exclude "sites/default"
		exclude ant.properties."site"
		exclude "sites/all/modules/dev"
		exclude "src/sites/all/data/"
		exclude ".git*"
	}
	into "${buildApplicationRoot}"
}

task generateSettings(type: Copy)  {
    inputs.files propertyFiles
    fileMode 0444
    dirMode 0555
    from("templates/") {
        include "${ant.properties.settingsTemplate}.skel"
    }
    into "${buildApplicationRoot}/sites/${ant.properties.site}"
    rename {name->"settings.php"}
    filter(ReplaceTokens, tokens: ant.properties)
}

generateSettings.doFirst {
        runCmd("/bin/rm -f ${buildApplicationRoot}/sites/${ant.properties.site}/settings.php", new File("."))
}

task generateVhost(type: Copy) {
    inputs.files propertyFiles
    fileMode 0444
    dirMode 0555
    from("templates/"){
        include "vhost.skel"
    }
    into "${ant.properties.target}/etc/"
    rename {name->"vhost-${ant.properties.site}.conf"}
    filter(ReplaceTokens, tokens: ant.properties)
}

generateVhost.doFirst {
    runCmd("/bin/rm -f target/etc/vhost-${ant.properties.site}.conf", new File("."))
}


task createFilesSymlinks(dependsOn: "generateSettings") << {
    runCmd("/bin/ln -sfn ${ant.properties."drupal.filesTarget"} ${ant.properties."drupal.filesDir"}", new File(buildApplicationRoot))
}

ext.dbUrl = {
    "mysql://${ant.properties."db.adminuser"}:${ant.properties."db.adminpass"}@${ant.properties."db.host"}/${ant.properties."db.name"}"
}

task buildSite(dependsOn: ["copySrc", "generateSettings", "generateVhost", "createFilesSymlinks"]) {

}

task installDrupal(dependsOn: buildSite) << {
    sql( sqlQuery: dropDatabaseSql)
    sql( sqlQuery: createDatabaseSql)
    ant.properties.webservers.split(',').each{ server ->
        sql( sqlQuery: grantPermissionsSql.replaceAll("@@HOST@@", server))
    }
    drush(
            drushCmd: "site-install ${ant.properties."drupal.profile"} " +
                "--sites-subdir=${ant.properties."site"} " +
                "--account-name=${ant.properties."drupal.adminuser"} " +
                "--account-pass=${ant.properties."drupal.adminpass"} " +
                "--site-mail='${ant.properties.mail}' " +
                "--site-name='${ant.properties."site"}' --yes"
    )

    if (ant.properties."drupal.modules"){
        drush(
                drushCmd: "pm-enable ${ant.properties."drupal.modules".split(',').join(' ')}"
        )
    }
}

task packageSite(dependsOn: buildSite) {
    inputs.dir ant.properties.build
    outputs.file "${ant.properties.target}/${ant.properties."application.name"}.tar.gz"
    doFirst{
        runCmd("mkdir -p ${ant.properties.target}", new File(ant.properties.build))
        runCmd("tar czf  ${ant.properties.target}/${ant.properties."application.name"}.tar.gz .", new File(buildApplicationRoot))
    }
}

task deployDrupal(dependsOn: packageSite) << {
    def servers = []
    servers << ant.properties."drush.host"
    ant.properties.webservers.split(",").each { servers << it   }
    servers.each{server ->
        copyFilesToServer(server)
    }
    servers.each{server ->
        createReleaseSymlinks(server)
        restartApache(server)
    }


}
